plugins {
    id 'java'
    id 'application'
}

// CI-friendly defaults -------------------------------------------------------
// Users can override the cache directory in CI with ENV var JAVACPP_CACHE
def javacppCacheDir = System.getenv('JAVACPP_CACHE') ?: "${System.getProperty('user.home')}/.javacpp/cache"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

application {
    mainClass = 'com.lansoftprogramming.runeSequence.Main'  // Minimal test class
    applicationDefaultJvmArgs = [
            "-Xms512m",
            "-Xmx2048m",
            "--enable-native-access=ALL-UNNAMED",
            "-Dorg.bytedeco.javacpp.cachedir=${javacppCacheDir}",
            "-Dorg.bytedeco.javacpp.logger=slf4j"
    ]
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.bytedeco:opencv-platform:4.10.0-1.5.11'
    implementation 'org.bytedeco:javacv:1.5.11'
    implementation 'org.bytedeco:ffmpeg-platform:7.1-1.5.11'

    //heybinds
    implementation 'com.1stleg:jnativehook:2.1.0'

    // Logging
    implementation 'org.slf4j:slf4j-api:2.0.17'
    implementation 'ch.qos.logback:logback-classic:1.5.21'

    // JSON
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.0'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.20'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.20.0'

    // GUI
    implementation 'com.formdev:flatlaf:3.6.2'

    // Native window helpers (click-through overlays on Windows)
    implementation 'net.java.dev.jna:jna:5.16.0'
    implementation 'net.java.dev.jna:jna-platform:5.16.0'
    // Testing (JUnit Jupiter 5)
    testImplementation platform('org.junit:junit-bom:5.11.3')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    // Explicit launcher keeps Gradle's JUnit Platform happy when versions advance
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    systemProperty 'java.awt.headless', 'true'          // Avoid UI requirement in CI
    // Keep JavaCPP native extraction/cache location stable and writable in CI/WSL.
    systemProperty 'org.bytedeco.javacpp.cachedir', javacppCacheDir
    jvmArgs += ["--enable-native-access=ALL-UNNAMED"]
    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
        showStandardStreams = System.getenv('CI') != null
        exceptionFormat 'short'
    }
}
tasks.withType(JavaExec).configureEach {
    jvmArgs += ["--enable-native-access=ALL-UNNAMED"]
    environment 'JAVACPP_CACHE', javacppCacheDir        // Keeps cache location consistent in CI
}
