plugins {
    id 'java'
    id 'application'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

application {
    mainClass = 'com.lansoftprogramming.runeSequence.Main'  // Minimal test class
    applicationDefaultJvmArgs = [
            "-Xms512m",
            "-Xmx2048m",
            "--enable-native-access=ALL-UNNAMED",
            "-Dorg.bytedeco.javacpp.cachedir=C:\\javacpp_cache",
            "-Dorg.bytedeco.javacpp.logger=DEBUG"
    ]
}

repositories {
    mavenCentral()
}

configurations.configureEach {
    resolutionStrategy {
        dependencySubstitution {
            // Force any CPU OpenCV preset to resolve to the GPU bundle
            substitute module("org.bytedeco:opencv-platform") using module("org.bytedeco:opencv-platform-gpu:4.11.0-1.5.12")
            substitute module("org.bytedeco:opencv") using module("org.bytedeco:opencv-platform-gpu:4.11.0-1.5.12")
        }
        eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'org.bytedeco' && details.requested.name.startsWith('opencv-platform') && !details.requested.name.contains('gpu')) {
                details.useTarget("org.bytedeco:opencv-platform-gpu:4.11.0-1.5.12")
            }
            if (details.requested.group == 'org.bytedeco' && details.requested.name == 'opencv') {
                details.useTarget("org.bytedeco:opencv-platform-gpu:4.11.0-1.5.12")
            }
        }
    }
}

dependencies {
    // Use CUDA-enabled OpenCV presets only. The GPU artifact includes CPU code as well,
    // so it still supports CPU fallback while allowing CUDA symbols to load correctly.
    implementation 'org.bytedeco:opencv-platform-gpu:4.11.0-1.5.12'
    implementation 'org.bytedeco:javacv:1.5.12'
    implementation 'org.bytedeco:ffmpeg-platform:7.1.1-1.5.12'
    // CUDA runtime bundle matching 1.5.12 presets (large download ~3GB)
    implementation 'org.bytedeco:cuda-platform-redist:12.9-9.10-1.5.12'

    //heybinds
    implementation 'com.1stleg:jnativehook:2.1.0'

    // Logging
    implementation 'org.slf4j:slf4j-api:2.0.17'
    implementation 'ch.qos.logback:logback-classic:1.5.21'

    // JSON
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.0'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.20'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.20.0'

    // GUI
    implementation 'com.formdev:flatlaf:3.6.2'
    //testing
    testImplementation 'org.junit.jupiter:junit-jupiter-api:6.0.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:6.0.1'
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}
tasks.withType(JavaExec).configureEach {
    jvmArgs += [
            "--enable-native-access=ALL-UNNAMED"
    ]
}

tasks.register('testOpenCV') {
    group = 'verification'
    description = 'Run a minimal OpenCV load test'
    dependsOn 'classes'
    doLast {
        javaexec {
            main = 'com.lansoftprogramming.runeSequence.TestLoad'
            classpath = sourceSets.main.runtimeClasspath
            jvmArgs = application.applicationDefaultJvmArgs
        }
    }
}
